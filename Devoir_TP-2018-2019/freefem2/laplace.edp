//verbosity 0;

// Trace de l'éxécution dans un fichier
ofstream tfile("output/laplace_trace_umfpack.txt");
// Fichier de sortie sous forme de tableau pour tracé gnuplot
ofstream gfile("output/laplace_errors_umfpack.txt");

tfile << "Probleme de Laplace" << endl;

gfile << "# Resolution de -Laplacien(u) = f avec u = 0 au bord" << endl;
gfile << "# sur le carre [0,1]x[0,1] en elements finis P1 Lagrange" << endl;
gfile << "# sur une suite de maillages" << endl;
gfile << "# et pour une solution analytique quadratique en x et y" << endl;

gfile << "# pas h, erreur L^2 relative, erreur relative L^2 sur le gradient, temps CPU" << endl;

// Définition des bord du domaine de calcul
border Gamma1(t=0,1) {x=t; y=0;};
border Gamma2(t=0,1) {x=1; y=t;};
border Gamma3(t=0,1) {x=1-t; y=1;};
border Gamma4(t=0,1) {x=0; y=1-t;};

// Boucle sur une famille de maillages de plus en plus fins
for (int k=0;k<6;++k) // Pour k=7, ça devient trop gros
  {
    // Construction du maillage
    int n = 10*2^k; // Nombre de points par côté
    real t1 = clock(); // Pour mesure du temps cpu
    mesh Th = buildmesh(Gamma1(n)+Gamma2(n)+Gamma3(n)+Gamma4(n));

    tfile << "k = " << k << endl;
    tfile << "  Nombre de sommets par cote : " << n << endl;
    tfile << "  Nombre de sommets dans le maillage : " << Th.nv << endl;
    tfile << "  Nombre de triangles dans le maillage : " << Th.nt << endl;

    // Affichage et sortie fichierdu maillage seul
    //plot(Gamma1(n)+Gamma2(n)+Gamma3(n)+Gamma4(n));
    //plot(Th, wait=1, ps="laplace.eps");

    // Définition de l'espace de discrétisation. Ici on discrétise
    // V=H^1_0(\Omega) avec des éléments finis P1-Lagrange
    fespace Vh(Th,P1);
    tfile << "  Nombre de degres de liberte : " << Vh.ndof << endl;

    cout << "*** k = " << k << " --> n = " << n << " , #DOf = " << Vh.ndof << " ***" << endl;

    // Définition de la formulation variationnelle dans Vh

    // On a besoin de 2 fonctions u et v dans Vh
    Vh u,v;
    // D'une fonction definie de manière analytique (pour le second membre)
    func f = 0.;
    func u1 = x;
    func u2 = 1+y;
    func u3 = x+1;
    func u4 = y;
    // Qui s'annule bien au bord du domaine et dont la solution (de
    // -Laplacien(u)=f) est $u(x,y) = x + y.

    // Voici finalement notre problème, avec son solveur linéaire
    problem laplace(u,v,solver=UMFPACK) = int2d(Th)( dx(u)*dx(v) + dy(u)*dy(v) )
      - int2d(Th)( f*v ) + on(Gamma1,u=u1) + on(Gamma2,u=u2) + on(Gamma3,u=u3) + on(Gamma4,u=u4);

    // Résolution du système linéaire 
    laplace;

    // Visualisation de la solution
    //plot(u, fill=true,value=true,boundary=true,wait=true);

    // Solution exacte projetee dans Vh
    Vh ue, dxue, dyue;
    ue = x+y; // Expression de la solution exacte
    dxue = 1;
    dyue = 1;

    // Calcul des erreurs
    real normeL2, errL2, normeH1, errH1;
    normeL2 = sqrt( int2d(Th) ( (ue)^2 ) );
    errL2 = sqrt( int2d(Th) ( (u-ue)^2 ) );

    normeH1 = sqrt( int2d(Th) ( (ue)^2 ) + int2d(Th) ( (dxue)^2 ) + int2d(Th) ( (dyue)^2 ));
    errH1   = sqrt( int2d(Th) ( (u-ue)^2 ) + int2d(Th) ( (dx(u)-dxue)^2 ) + int2d(Th) ( (dy(u)-dyue)^2 ));


    real t2 = clock(); // Pour mesure du temps cpu

    tfile << "  Erreur L2 relative : " << errL2/normeL2 << endl;
    tfile << "  Erreur H1 relative : " << errH1/normeH1 << endl;
    tfile << "  Temps CPU total : " << t2-t1 << endl;
    tfile << "  min(u) : " << u[].min << "  max(u) : " << u[].max << "  min(ue) : " << ue[].min << "  max(ue) : " << ue[].max << endl;


    gfile << 1./n << "  " << errL2/normeL2 << "  " << errH1/normeH1 << "  " << t2-t1 << endl;
//    gfile << 1./n << "  " << errH1/normeH1 << "  " << t2-t1 << endl;
  }
